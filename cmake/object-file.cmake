define_property(TARGET PROPERTY OBJECT_FILES
  BRIEF_DOCS "Output files generated by relocatably linking objects in a static library."
  FULL_DOCS "Output files generated by relocatably linking objects in a static library.")

# After building the static library, generate an object file in the current
# binary directory by relocatably linking together all the files in the library.
function(generate_object_file target output_name)
  get_target_property(type ${target} TYPE)
  if(NOT type STREQUAL STATIC_LIBRARY)
    message(FATAL_ERROR "generate_object_file called on unsupported target ${target} of type ${type}")
  endif()

  add_custom_command(TARGET ${target} POST_BUILD
    COMMAND ${CMAKE_LINKER} -r -o ${output_name} --whole-archive
            $<TARGET_FILE:${target}>)
  set_property(TARGET ${target} PROPERTY OBJECT_FILES ${CMAKE_CURRENT_BINARY_DIR}/${output_name} APPEND)
endfunction()

# Install generated object files in the library directory.
function(install_object_files target)
  get_property(object_files TARGET ${target} PROPERTY OBJECT_FILES)
  if (NOT object_files)
    message(FATAL_ERROR "No generated object files found for ${target}.")
  endif()

  install(FILES ${object_files} TYPE LIB)
endfunction()
